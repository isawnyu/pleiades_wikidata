SELECT DISTINCT ?pleiades ?item ?itemLabel ?itemDescription ?coordinates (GROUP_CONCAT(DISTINCT ?chronique; SEPARATOR=",") as ?chronique_ids) (GROUP_CONCAT(DISTINCT ?dare; SEPARATOR=",") as ?dare_ids) (GROUP_CONCAT(DISTINCT ?geonames; SEPARATOR=",") as ?geonames_ids) (GROUP_CONCAT(DISTINCT ?gettytgn; SEPARATOR=",") as ?gettytgn_ids) (GROUP_CONCAT(DISTINCT ?idaigaz; SEPARATOR=",") as ?idaigaz_ids) (GROUP_CONCAT(DISTINCT ?loc; SEPARATOR=",") as ?loc_ids) (GROUP_CONCAT(DISTINCT ?manto; SEPARATOR=",") as ?manto_ids) (GROUP_CONCAT(DISTINCT ?nomisma; SEPARATOR=",") as ?nomisma_ids) (GROUP_CONCAT(DISTINCT ?topostext; SEPARATOR=",") as ?topostext_ids) (GROUP_CONCAT(DISTINCT ?trismegistos; SEPARATOR=",") as ?trismegistos_ids) (GROUP_CONCAT(DISTINCT ?viaf; SEPARATOR=",") as ?viaf_ids) (GROUP_CONCAT(DISTINCT ?vici; SEPARATOR=",") as ?vici_ids) ?wikipedia_en WHERE {
  ?item wdt:P1584 ?pleiades .
  OPTIONAL {
    ?item wdt:P625 ?coordinates .
  }
  OPTIONAL {
    ?item wdt:P1687 ?chronique .
  }
  OPTIONAL {
    ?item wdt:P1936 ?dare .
  }
  OPTIONAL {
    ?item wdt:P1566 ?geonames .
  }
  OPTIONAL {
    ?item wdt:P1667 ?gettytgn .
  }
  OPTIONAL {
    ?item wdt:P8217 ?idaigaz . 
  }
  OPTIONAL {
    ?item wdt:P244 ?loc .
  }
  OPTIONAL {
    ?item wdt:P9736 ?manto .
  }
  OPTIONAL {
    ?item wdt:P2950 ?nomisma .
  }
  OPTIONAL {
    ?item wdt:P8068 ?topostext .
  }
  OPTIONAL {
    ?item wdt:P1958 ?trismegistos .
  }
  OPTIONAL {
    ?item wdt:P214 ?viaf .
  }
  OPTIONAL {
    ?item wdt:P1481 ?vici .
  }
  OPTIONAL {
    ?wikipedia_en schema:about ?item .
    ?wikipedia_en schema:isPartOf <https://en.wikipedia.org/>.
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
  {
    SELECT DISTINCT ?item WHERE {
      ?item p:P1584 ?statement0.
      ?statement0 (ps:P1584) _:anyValueP1584.
    }
  }
} GROUP BY ?pleiades ?item ?itemLabel ?itemDescription ?coordinates ?wikipedia_en
ORDER BY ?pleiades ?item
